plugins {
    id "com.github.johnrengelman.shadow" version "2.0.1"
    id 'org.sonarqube' version '2.5'
    id "io.saagie.gradle-saagie-plugin" version "2.0.5"
}

group 'io.saagie'
version outisVersion

subprojects {
    apply plugin: 'scala'
    apply plugin: 'jacoco'

    repositories {
        jcenter()
    }
}

project(':outis-core') {
    dependencies {
        compile group: "org.slf4j", name: "slf4j-log4j12", version: "1.7.25"
        compile group: "com.typesafe", name: "config", version: "1.3.0"
        compile group: "com.github.scopt", name: "scopt_$scalaMajor", version: scoptVersion
        compile group: "com.databricks", name: "spark-avro_$scalaMajor", version: "3.2.0"

        compileOnly group: "org.apache.spark", name: "spark-core_$scalaMajor", version: sparkVersion
        compileOnly group: "org.apache.spark", name: "spark-sql_$scalaMajor", version: sparkVersion

        testCompile group: "org.scalatest", name: "scalatest_$scalaMajor", version: "3.0.4"
    }
}

project(':outis-link') {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'io.saagie.gradle-saagie-plugin'

    ext.mainClassName = 'io.saagie.outis.link.DatagovClient'

    dependencies {
        compile project(':outis-core')

        compile group: 'org.scala-lang', name: 'scala-library', version: "$scalaMajor.$scalaMinor"
        compile group: 'com.squareup.okhttp3', name: 'okhttp', version: okHttpVersion
        compile group: 'org.json4s', name: "json4s-native_$scalaMajor", version: json4sVersion

        compileOnly group: "org.apache.spark", name: "spark-core_$scalaMajor", version: sparkVersion
        compileOnly group: "org.apache.spark", name: "spark-sql_$scalaMajor", version: sparkVersion
    }

    shadowJar {
        zip64 = true
        classifier = ""
        manifest {
            attributes "Main-Class": mainClassName
        }
    }
}
